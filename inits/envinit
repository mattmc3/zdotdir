#!/bin/zsh

# Set XDG locations
if [[ "$1" == --use-xdg-base-dirs ]]; then
  export XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-$HOME/.config}
  export XDG_CACHE_HOME=${XDG_CACHE_HOME:-$HOME/.cache}
  export XDG_DATA_HOME=${XDG_DATA_HOME:-$HOME/.local/share}
  export XDG_STATE_HOME=${XDG_STATE_HOME:-$HOME/.local/state}
fi

# Ensure path arrays do not contain duplicates.
typeset -gaU cdpath fpath mailpath path prepath

# Set the list of directories that Zsh searches for programs.
[[ -n "$prepath" ]] || prepath=(
  $HOME/{,s}bin(N)
  $HOME/.local/{,s}bin(N)
)

path=(
  $prepath
  /opt/{homebrew,local}/{,s}bin(N)
  /usr/local/{,s}bin(N)
  $path
)

# Setup homebrew if it exists on the system.
local -aU brewcmd=(
  $commands[brew](N)
  $HOME/.homebrew/bin/brew(N)
  $HOME/.linuxbrew/bin/brew(N)
  /opt/homebrew/bin/brew(N)
  /usr/local/bin/brew(N)
)
if (( ${#brewcmd} )); then
  # Default to no tracking.
  HOMEBREW_NO_ANALYTICS="${HOMEBREW_NO_ANALYTICS:-1}"

  # Initialize homebrew.
  source <($brewcmd[1] shellenv)

  # Preserve the desired path order.
  path=($prepath $path)
fi

# Set reasonable defaults for common shell variables.
export BROWSER="${BROWSER:-open}"
export EDITOR="${EDITOR:-nvim}"
export VISUAL="${VISUAL:-code}"
export PAGER="${PAGER:-less}"
export LANG="${LANG:-en_US.UTF-8}"
export LESS="${LESS:--g -i -M -R -S -w -z-4}"

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if [[ -z "$LESSOPEN" ]] && (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

# Make Terminal.app behave.
if [[ "$OSTYPE" == darwin* ]]; then
  export SHELL_SESSIONS_DISABLE="${SHELL_SESSIONS_DISABLE:-1}"
fi
