#!/bin/zsh
#
# histinit: Set history options and define history aliases.
#

# Set history options.
setopt bang_hist               # Treat the '!' character specially during expansion.
setopt extended_history        # Write the history file in the ':start:elapsed;command' format.
setopt hist_expire_dups_first  # Expire a duplicate event first when trimming history.
setopt hist_find_no_dups       # Do not display a previously found event.
setopt hist_ignore_all_dups    # Delete an old recorded event if a new event is a duplicate.
setopt hist_ignore_dups        # Do not record an event that was just recorded again.
setopt hist_ignore_space       # Do not record an event starting with a space.
setopt hist_reduce_blanks      # Remove extra blanks from commands added to the history list.
setopt hist_save_no_dups       # Do not write a duplicate event to the history file.
setopt hist_verify             # Do not execute immediately upon history expansion.
setopt inc_append_history      # Write to the history file immediately, not when the shell exits.
setopt NO_hist_beep            # Don't beep when accessing non-existent history.
setopt NO_share_history        # Don't share history between all sessions.

# Set the path to the default history file.
if zstyle -s ':zsh:history' HISTFILE 'HISTFILE'; then
  HISTFILE="${~HISTFILE}"  # Convert leading '~' to $HOME
elif [[ -n "$XDG_DATA_HOME" ]]; then
  HISTFILE="${XDG_DATA_HOME}/zsh/zsh_history"
else
  HISTFILE="${ZDOTDIR:-$HOME}/.zsh_history"
fi

# Make sure the HISTFILE's directory exists.
[[ -d "${HISTFILE:h}" ]] || mkdir -p "${HISTFILE:h}"

# Set history file size.
zstyle -s ':zsh:history' SAVEHIST 'SAVEHIST' \
  || SAVEHIST=100000

# Set session history size.
zstyle -s ':zsh:history' HISTSIZE 'HISTSIZE' \
  || HISTSIZE=20000

# Use a better history command.
alias history='fc -li'
