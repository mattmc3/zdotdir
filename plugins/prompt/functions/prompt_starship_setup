#
# prompt_starship_setup
#

#
# Requirements
#

0=${(%):-%x}
if ! (( $+commands[starship] )); then
  echo >&2 "Starship prompt not installed. See https://starship.rs to install."
  return 1
fi

# Set the config. The logic is:
# - Favor the function argument
# - Then favor custom themes dir, starship dir, and finally Zephyr
if [[ -n "$1" ]]; then
  export STARSHIP_CONFIG=${0:A:h:h}/themes/${1}.toml
else
  zstyle -s ':zsh_custom:plugin:prompt:starship' config prompt_config
  if [[ -n "$prompt_config" ]]; then
    local -a starship_cfgs=(
      ${ZDOTDIR:-${XDG_CONFIG_HOME:-$HOME/.config}/zsh}/themes/${prompt_config}.toml(N)
      ${XDG_CONFIG_HOME:-$HOME/.config}/starship/${prompt_config}.toml(N)
    )
    (( ! $#starship_cfgs )) || export STARSHIP_CONFIG=$starship_cfgs[1]
  else
    unset STARSHIP_CONFIG
  fi
fi

#eval "$(starship init zsh)"
source <(starship init zsh --print-full-init)

# vim: ft=zsh sw=2 ts=2 et
