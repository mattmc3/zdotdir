##? funced - edit the function specified

#function funced {

# check args
if [[ -z "$1" ]]; then
  echo >&2 "funced: Expected function name argument"
  return 1
elif [[ ! -d "$ZFUNCDIR" ]]; then
  echo >&2 "funced: Directory not found '$ZFUNCDIR'."
  return 1
fi

# new function definition: make a file template
if [[ ! -f "$ZFUNCDIR/$1" ]]; then
  cat <<eos > "$ZFUNCDIR/$1"
##############################################################################
# TODO: Remove this header block
# Do NOT include function definition block (ie: omit 'function $1() { }').
# See: https://zsh.sourceforge.io/Doc/Release/Functions.html#Autoloading-Functions
#
# Use ##? comments for help/usage docs.
##############################################################################

##? $1 - TODO: add description here
##?
##? usage: $1 [-h|--help]
##?        $1 [<options>] <arguments>

#function $1 {

local -a o_help
zmodload zsh/zutil
zparseopts -D -F -K -- \
  {h,-help}=o_help ||
  return 1

if (( \$#o_help )); then
  funchelp "$1"
  return
fi

# TODO: write your function here

#}

eos
  autoload -Uz "$ZFUNCDIR/$1"
fi

# open the function file
if [[ -z "$VISUAL" ]]; then
  $VISUAL "$ZFUNCDIR/$1"
else
  ${EDITOR:-vim} "$ZFUNCDIR/$1"
fi

#}

# vim: ft=zsh sw=2 ts=2 et
