#!/bin/zsh
# Wrap starship to work with built-in prompt system.
function prompt_starship_setup {
  if (( ! $+commands[starship] )); then
    print -ru2 "starship command not found"
    return 1
  fi

  # When loaded through the prompt command, these prompt_* options will be enabled
  prompt_opts=(cr percent sp subst)

  # Set the starship config based on the argument if provided.
  if [[ -n "$1" ]]; then
    local -a configs=(
      "$ZSH_CONFIG_DIR/themes/${1}.toml"(N)
      "${XDG_CONFIG_HOME:-$HOME/.config}/starship/${1}.toml"(N)
    )
    (( $#configs )) && export STARSHIP_CONFIG=$configs[1]
  fi

  # Initialize starship.
  source <(starship init zsh)
}
prompt_starship_setup "$@"
