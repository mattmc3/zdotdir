#!/bin/zsh

##? clone - clone a git repo
if [[ -z "$1" ]]; then
  echo "What git repo do you want?" >&2
  return 1
fi
local user repo
if [[ "$1" = */* ]]; then
  user=${1%/*}
  repo=${1##*/}
else
  # Use the git user zstyle
  zstyle -s ':zsh:custom:git' default-user user || user=mattmc3
  if [[ -z "$user" ]]; then
    echo "clone: Expecting zstyle ':zsh:custom:git' default-user." >&2
    return 1
  fi
  repo=$1
fi

local giturl
zstyle -s ':zsh:custom:git' default-site giturl || giturl=github.com
local dest=${XDG_PROJECTS_HOME:-~/Projects}/$user/$repo

if [[ ! -d $dest ]]; then
  git clone --recurse-submodules "git@${giturl}:${user}/${repo}.git" "$dest"
else
  echo "No need to clone, that directory already exists."
  echo "Taking you there."
fi
cd $dest
