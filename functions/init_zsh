#!/bin/zsh
#function zsh_init {

[[ -z "$ZPROFINIT" ]] || zmodload zsh/zprof
alias zprofinit="ZPROFINIT=1 zsh"

# external repos
local REPOS=${REPO_HOME:=${XDG_CACHE_HOME:=~/.cache}/repos}
[[ -d $REPOS ]] || repo in <$ZDOTDIR/repos.txt

# prezto func
function pmodload {
  # no-op
}

# prezto zstyles
zstyle ':prezto:*:*' color 'yes'
zstyle ':prezto:module:autosuggestions:color' found 'fg=60'
zstyle ':prezto:module:*:alias' skip 'yes'
zstyle ':prezto:module:history' histfile $XDG_DATA_HOME/zsh/history

# editor
zstyle ':prezto:module:editor' key-bindings 'emacs'
zstyle ':prezto:module:editor' dot-expansion 'yes'
zstyle :zle:edit-command-line editor hx

# plugin configs
ABBR_USER_ABBREVIATIONS_FILE=$ZDOTDIR/.zabbr
MAGIC_ENTER_GIT_COMMAND='git status -sb'
MAGIC_ENTER_OTHER_COMMAND='ls'
ZSH_COMPDUMP=$XDG_CACHE_HOME/prezto/zcompdump
_Z_DATA=$XDG_DATA_HOME/z/data
[[ -d $_Z_DATA:h ]] || mkdir -p $_Z_DATA:h

# Plugin dirs
local PREZTO=$REPOS/sorin-ionescu/prezto/modules
local OMZ=$REPOS/ohmyzsh/ohmyzsh/plugins
local ZSH_UTILS=$REPOS/belak/zsh-utils
local MY=$ZDOTDIR/plugins

source $REPOS/sorin-ionescu/prezto/runcoms/zprofile
source $REPOS/ohmyzsh/ohmyzsh/lib/clipboard.zsh

local -a plugins=(
  $ZSH_UTILS/prompt
  $ZSH_UTILS/utility
  $OMZ/copybuffer
  $OMZ/copyfile
  $OMZ/copypath
  $PREZTO/helper
  $PREZTO/terminal
  $OMZ/magic-enter
  $OMZ/fancy-ctrl-z
  $PREZTO/editor
  $PREZTO/history
  $PREZTO/directory
  $PREZTO/environment
  $PREZTO/homebrew
  $PREZTO/osx
  $REPOS/rupa/z
  $MY/python
  $MY/git
  $PREZTO/completion
)
for plugin in $plugins; do
  source $plugin/${plugin:t}.plugin.zsh
done

# Set prompt
prompt starship

local -a defer_plugins=(
  $REPOS/mattmc3/zman
  $REPOS/olets/zsh-abbr
  $REPOS/zsh-users/zsh-autosuggestions
  $REPOS/zsh-users/zsh-history-substring-search
  $REPOS/zdharma-continuum/fast-syntax-highlighting
)
for plugin in $defer_plugins; do
  zsh-defer source $plugin/${plugin:t}.plugin.zsh
done

# Fix prezto settings
setopt NO_beep NO_hist_beep prompt_subst

# Done profiling.
[[ -z "$ZPROFINIT" ]] || zprof
unset ZPROFINIT

#}
